#!/usr/bin/env perl

use strict;
use warnings;
use Getopt::Long qw(GetOptions);
use File::Basename;
use Date::Manip;
use Cwd 'abs_path';
use JSON qw(encode_json decode_json);

use RSVMetric;

print_output_header();

our $CRON_D = '/etc/cron.d';


######## Retrieve Global variables ################################
our %o;
our %metric;
# Set $run_me_root to RMR_IF_NOT_OK if conditions arise such that we should give
# local probe run instructions in the event of a problem. Set to RMR_ALWAYS if
# we should give local probe run instructions regardless of metricStatus.
use constant { RMR_NEVER => 0, RMR_IF_NOT_OK => 1, RMR_ALWAYS => 2 };
use constant { STATUS_OK => 0, STATUS_WARNING => 1, STATUS_CRITICAL => 2, STATUS_UNKNOWN => 3 };
my $run_me_root = RMR_NEVER;
my ($is_root, $probeconfig_problems, $soaphost, $probename,
    $sitename, $enabled, $datafolder, $workingfolder, $real_datafolder,
    $current_gratia_probe_type,
    $record_count, $file_count, $archive_count, $old_file_count,
    $real_gratia_probe_type
   );
use constant { STATUS_OK => 0, STATUS_WARNING => 1, STATUS_CRITICAL => 2, STATUS_UNKNOWN => 3 };

my %extra_opts = ("discover"       => "<Show available probes on this system>",
                  "human-readable" => "<Print output in readable format>");

my %options = process_command_line(%extra_opts);

$options{'discover'} ||= 0;
$options{'human-readable'} ||= 0;
$metric{metricStatus} ||= STATUS_OK;

($o{'gratiaProbeType'} = $options{'m'}) =~ s/^org\.osg\.gratia\.//;

$metric{metricStatus} = STATUS_OK;

Run();

# force glob to run in list context but return scalar result
sub globcount {
  scalar @{[ glob $_[0] ]}
}

sub Run {

  if ($options{discover}) {
    &discoverProbesToTest();
  }

  $is_root = not $<;		# Are we running as root?

  # Probe type
  $current_gratia_probe_type = gratiaProbeType();
  $real_gratia_probe_type = ($current_gratia_probe_type =~ /pbs|lsf/) ? 'pbs-lsf' : $current_gratia_probe_type;
  unless ($current_gratia_probe_type) {
    setCritical();
    &appendDetailsData(<<EOF);
ERROR: Probe type must be set with -p! Run with --discover (add -X for
human-readable output) to find possible values on this system.
EOF
    normal_exit()
  }

  ####################################
  # Start tests

  if($ENV{OSG_LOCATION}) {
    my @PYTHONPATH = ();
    @PYTHONPATH = split /:/, $ENV{PYTHONPATH} if  $ENV{PYTHONPATH};
    unless (grep m&^\Q$ENV{OSG_LOCATION}/gratia/probe/common\E$&, @PYTHONPATH) {
      push @PYTHONPATH, "$ENV{OSG_LOCATION}/gratia/probe/common";
      $ENV{PYTHONPATH} = join(":", @PYTHONPATH);
    }

    $ENV{PATH} = "$ENV{PATH}:$ENV{OSG_LOCATION}/wget/bin";
  }

  # Check directories
  unless (checkProbeDirs()) {
    setUnknown();
    normal_exit();
  }

  $soaphost = getProbeConfigAttribute("SOAPHost");
  if (!defined($soaphost) || length($soaphost) == 0) {
    $soaphost = getProbeConfigAttribute("CollectorHost");
  }
  $probename = getProbeConfigAttribute("MeterName");
  if (!defined($probename) || length ($probename) == 0) {
    $probename = getProbeConfigAttribute("ProbeName");
  }
  $sitename = getProbeConfigAttribute("SiteName");
  $enabled = getProbeConfigAttribute("EnableProbe");
  $datafolder = getProbeConfigAttribute("DataFolder");
  $datafolder =~ s&/$&&;
  $workingfolder = getProbeConfigAttribute("WorkingFolder");
  $real_datafolder = abs_path($datafolder);

  ################################
  # Check some basic critical stuff

  # Is probe in cron.d (if it's supposed to be)?
  if ($current_gratia_probe_type =~
      m&^(?:sge|pbs|lsf|pbs-lsf|condor|glexec)&i) {

    # See if cron.d is accessible (on EL5 it might not be, see SOFTWARE-1602)
    my $username = getpwuid($<);
    if (! -x $CRON_D) {
      &appendDetailsData("NOTE: Cron directory $CRON_D is not accessible by the $username user.\n");
      &appendDetailsData("Cannot determine if $probename is enabled.\n");
      $run_me_root = RMR_IF_NOT_OK;
    } else {
      my $cron_grep_pattern = '^[[:space:]]*[^#].*gratia/\(probe/\)\?'.$real_gratia_probe_type.'/';
      my $quoted_cron_grep_pattern = shellquote($cron_grep_pattern);

      # Use -q so we get a 0 return code if grep finds the pattern but there
      # are other files it could not read. Without -q, the return code would be
      # 2. With -q, the return code is 2 only if grep does not find the pattern
      # and could not read some files.
      my $ret = system("grep -q -e $quoted_cron_grep_pattern '$CRON_D'/* >/dev/null 2>&1") >> 8;

      if ($ret == 1) {
        # could read all the files but didn't find the line in any of them
        setCritical();
        &appendDetailsData("ERROR: Probe $probename is not listed in $CRON_D.\n");
      } elsif ($ret == 2) {
        # grep couldn't read some files AND didn't find the line in the files grep could read
        &appendDetailsData("NOTE: Some files in $CRON_D are not accessible by the $username user.\n");
        &appendDetailsData("Cannot determine if $probename is enabled.\n");
        $run_me_root = RMR_IF_NOT_OK;
      }
    }
  }
  # Is probe enabled?
  if ($enabled ne "1") {
    setCritical();
    &appendDetailsData("ERROR: Probe $probename has EnableProbe not set to 1.\n");
    $run_me_root = RMR_IF_NOT_OK;
    $probeconfig_problems = 1;
  }
  # Is probename generic or unset?
  if ($probename =~ m&(?:generic|unknown)&i) {
    setCritical();
    &appendDetailsData("ERROR: MeterName $probename appears to be generic (critical error).\n");
    $probeconfig_problems = 1;
    normal_exit();
  }
  if (! $datafolder) {
    setCritical();
    &appendDetailsData("ERROR: Probe $probename has DataFolder unset (critical error).\n");
    $probeconfig_problems = 1;
    normal_exit();
  }
  if (! $workingfolder) {
    setCritical();
    &appendDetailsData("ERROR: Probe $probename has WorkingFolder unset (critical error).\n");
    $probeconfig_problems = 1;
    normal_exit();
  }

  # Do we have the correct permissions on the datafolder?
  my (@rdf_stat) = stat $real_datafolder;
  if ($current_gratia_probe_type =~ m&^(?:sge|pbs|lsf|pbs-lsf|condor)&i) {
    if ($rdf_stat[2] != 041777) {
      setCritical();
      &appendDetailsData(sprintf("%s (found %05o, wanted %05o).",
                                 "WARNING: Data folder $real_datafolder does not have correct permissions (critical error).\n",
                                 $rdf_stat[2],
                                 041777));
    }
  }

  #################################
  # Check format of MeterName
  my @colon_split = split /:/, $probename;
  my $num_colons = $#colon_split;
  if ($num_colons != 1) {
    setWarning();
    &appendDetailsData("WARNING: Not one and only one \`:' in MeterName $probename.\n");
    $probeconfig_problems = 1;
  }

  ################################
  # Check sitename configuration.
  if ((!$sitename) or $sitename =~ m&(?:generic|unknown)&i) {
    setWarning();
    &appendDetailsData (<<EOF);
WARNING: Configured SiteName "$sitename" is empty or generic.
EOF
    $probeconfig_problems = 1;
  }

  ################################
  # Any checks specific to the probe type
  probe_specific_checks();

  ################################
  # Check size of reprocessing queue
  init_find_counts();

  # This replicates the behavior of OpenNewRecordFile() from gratia/common/sandbox_mgmt.py, Gratia version 1.13.9
  # which uses ProbeConfiguration.getFilenameFragment() from gratia/common/common/probe_config.py
  # If that changes, this might become invalid, hence the warning if the directory does not exist.
  # See also SOFTWARE-535.
  my $subdir = "subdir." . ProbeConfiguration_getFilenameFragment($probename, $soaphost);
  $subdir = "$workingfolder/gratiafiles/$subdir";
  if(!-e $subdir) {
      setWarning();
      my $message = "WARNING: The expected directory containing records waiting to be re-sent does not exist ($subdir).  This might be a problem in the gratia-config-probe itself.  Please report this error to OSG for assistance.\n";
      appendDetailsData($message);
  }

  find( { wanted => \&process_find_leaf, follow => 1, follow_skip => 2 }, $subdir);
  my $message="$record_count records waiting to be re-sent to $soaphost in $file_count files (including $archive_count archive files).\n";
  if ($record_count > 1000) {
    setWarning();
    $message = "WARNING: $message";
  }
  &appendDetailsData ($message);
  if ($old_file_count > 0) {
    setWarning();
    &appendDetailsData (sprintf("WARNING: %d %s > 7 days old: please verify CollectorHost setting in ProbeConfig and collector status.\n",
                        $old_file_count, ($old_file_count>1?"files are":"file is")));
  }

  ################################
  # Check number of files in data area, per file type (SOFTWARE-563).

  my $n_certinfo_files   = globcount("\Q$datafolder\E/gratia_certinfo_*");
  my $n_classad_records  = globcount("\Q$datafolder\E/history.*");
  my $n_quarantine_files = globcount("\Q$datafolder\E/quarantine/*");
  my @unsent_records     = glob("\Q$workingfolder\E/*/*");
  my $n_unsent_records   = @unsent_records;
  my %unsent_by_dir;
  for my $rec (@unsent_records) {
    my ($dir) = ($rec =~ m{(.+)/[^/]+$});
    ++$unsent_by_dir{$dir};
  }
  $unsent_by_dir{$workingfolder} = 0 if $n_unsent_records == 0;

  my $ndata = $n_certinfo_files + $n_classad_records + $n_quarantine_files 
            + $n_unsent_records;

  $message = join("\n  ",
    "$ndata total gratia files found:",
    "$n_certinfo_files gratia_certinfo files under $datafolder/",
    "$n_classad_records classad records under $datafolder/",
    "$n_quarantine_files quarantine files under $datafolder/quarantine/",
    map("$unsent_by_dir{$_} unsent collector records under $_",
        sort keys %unsent_by_dir)
  ) . "\n";

  if ($ndata == 0 || $ndata > 10000) {
    setWarning();
    $message="WARNING: $message";
  }
  &appendDetailsData ($message);

  ################################
  # Check last contact time with Gratia server.
  my @cur_time= localtime();
  # localtime gives years from 1900
  my $year = 1900 + $cur_time[5];
  # perl uses 0 based months
  my $month = 1 + $cur_time[4];
  my $gracchost = "https://gracc.opensciencegrid.org";
  my $graccindex = sprintf "gracc.osg.raw-%04d.%02d", $year, $month;
  my $es_query = { query => {
                     query_string => {
                       query => "ProbeName:\"$probename\" AND EndTime:[now-2d TO now]"
                     },
                   },
                   sort => [
                     {
                       '@received' =>  {
                         order => "desc",
                       },
                     },
                   ],
                   size => 1
                 };
  my $es_json = encode_json $es_query;
  my $curl_cmdline = shellquote(
    qw(curl -q --retry 1  --connect-timeout 10 -XGET ),
    "$gracchost/q/$graccindex/_search",
    "-d",
    $es_json
  );
  my $probe_contact_check = `$curl_cmdline`;
  chomp $probe_contact_check;
  # rename field to make life easier later
  $probe_contact_check =~ s/\@received/received/;
  # Check to see if we've ever had a contact with this collector
  my $response = decode_json($probe_contact_check);
  if (exists $$response{hits}{total}) {
    if ($$response{hits}{total} == 0) {
      setCritical();
      $run_me_root = RMR_IF_NOT_OK;
      &appendDetailsData (<<EOF);
ERROR: Probe $probename has not been seen by Gratia collector $gracchost in more than two days.
EOF
      normal_exit();
    }
  } else {
    setWarning();
    &appendDetailsData (<<EOF);
WARNING: GRACC server $gracchost not responding.
$curl_cmdline
EOF
  }

  my $probe_seen_by_gracchost = 0;
  if (exists $$response{hits}{hits}[0]{_source}{received}) {
    my $probe_contact_date = ParseDate($$response{hits}{hits}[0]{_source}{received});
    my $last_contact_string = UnixDate($probe_contact_date, "%Y-%m-%d %T %Z");
    my $cmpDateHour = DateCalc("today", "- 1 hour");
    my $cmpDateDay = DateCalc("today", "- 1 day");
    if (Date_Cmp($probe_contact_date, $cmpDateDay) < 1) {
      setCritical();
      $run_me_root = RMR_IF_NOT_OK;
      &appendDetailsData (<<EOF);
ERROR: Probe $probename has not been seen by Gratia collector $gracchost in >1d.
Last contact time = $last_contact_string.
EOF
      normal_exit();
    } elsif (Date_Cmp($probe_contact_date, $cmpDateHour) < 1) {
      setWarning();
      $run_me_root = RMR_IF_NOT_OK;
      &appendDetailsData (<<EOF);
WARNING: Probe $probename has not been seen by Gratia collector $gracchost in >1h.
Last contact time = $last_contact_string
EOF
    } else {
      $probe_seen_by_gracchost = 1;
      &appendDetailsData (<<EOF);
Probe $probename has sent data to Gratia collector within the last hour.
EOF
    }
  }

  ################################
  # Cross-check locally configured site name matches Gratia server sitename.
  # Note: don't do this for metric probes going to the GOC.
  if ($probe_seen_by_gracchost && $current_gratia_probe_type ne "metric") {
    my $es_query2 = {
      query => {
        query_string => {
          query => "SiteName:\"$sitename\" AND ProbeName:\"$probename\" AND EndTime:[now-2d TO now]"
        }
      },
      sort => [
        {
          '@received' => {
            order => "desc"
          }
        }
      ],
      size => 1
    };
    my $es_json2 = encode_json $es_query2;
    my $curl_cmd = shellquote(
      qw(curl -q --retry 1  --connect-timeout 10 -XGET ),
      "$gracchost/q/$graccindex/_search",
      "-d",
      $es_json2
    );
    my $probe_site_check = `$curl_cmd`;
    my $response2 = decode_json $probe_site_check;
    if ($$response2{hits}{total} == 0) {
      setWarning();
      &appendDetailsData (<<EOF);
WARNING: Probe $probename not found in check of probes reporting to $gracchost from $sitename.
Check Collector SiteName translation on GRACC

EOF
    } else {
      &appendDetailsData (<<EOF);
Configured site name ($sitename) for probe $probename matches translation.
EOF
    }
  }

  ################################
  # Check log file for jobs reported.

  normal_exit(1);
}


sub runMeRoot {
  &appendDetailsData (<<EOF);
Some information could not be gathered due to lack of privileges.  Install and
run the latest version of osg-system-profiler for more diagnostic details,
especially if you have a WARNING or CRITICAL status.
EOF
}

sub probeConfigProblems {
  &appendDetailsData (<<EOF);
Please check ProbeConfig for problems.
EOF
}

sub normal_exit {
  return if $o{test} and not $_[0];
  probeConfigProblems() if $probeconfig_problems;
  if (not $is_root and
      ((RMR_IF_NOT_OK == $run_me_root and $metric{metricStatus} != STATUS_OK) or
       (RMR_ALWAYS == $run_me_root))) {
    runMeRoot();
  }
  ## Print metric, and we're all done!
  if ($options{'human-readable'}) {
    # Want human-readable results
    my $statusWord = metricStatusWord();
    workerScriptOutput(0, <<EOF, "");
STATUS: $statusWord
DETAILS:
$metric{'detailsData'}
EOF
  } else {
    workerScriptOutput(0,"$metric{'metricStatus'}\n$metric{'detailsData'}\n","");
  }
}


sub setCritical {
  $metric{'metricStatus'} = STATUS_CRITICAL;
}

sub setWarning {
  $metric{'metricStatus'} = STATUS_WARNING unless
    $metric{'metricStatus'} == STATUS_CRITICAL;
}

sub setUnknown {
  $metric{'metricStatus'} = STATUS_UNKNOWN unless
    $metric{'metricStatus'} == STATUS_CRITICAL;
}

sub metricStatusWord {
    if (STATUS_OK == $metric{'metricStatus'}) {
        return "OK";
    }
    elsif (STATUS_WARNING == $metric{'metricStatus'}) {
        return "WARNING";
    }
    elsif (STATUS_CRITICAL == $metric{'metricStatus'}) {
        return "CRITICAL";
    }
    elsif (STATUS_UNKNOWN == $metric{'metricStatus'}) {
        return "UNKNOWN";
    }
    else {
        # Fallback
        return $metric{'metricStatus'};
    }
}

sub probe_specific_checks {
  if ($current_gratia_probe_type =~ m&(?:pbs|pbs-lsf|lsf)&i) {
    # PBS / LSF probe checks
    probe_check_pbs_lsf();
  } elsif ($current_gratia_probe_type eq "condor") {
    probe_check_condor();
  }
}

sub probe_check_condor() {
  my $get_condor_config_cmd = "condor_config_val PER_JOB_HISTORY_DIR";

  if($ENV{OSG_LOCATION}) {
      $get_condor_config_cmd = '. "$OSG_LOCATION"/setup.sh;' . " $get_condor_config_cmd";
  }
  else {
      # If Condor is not installed in the default location we will go looking for it.
      if (system("which condor_config_val >/dev/null 2>&1") != 0) {
          open(IN, '<', '/etc/globus/globus-condor.conf');
          my $condor_config_val;
          my $condor_config;
          while(<IN>) {
              if(/^condor_submit\s*=\s*\"(.+)\"/ ) {
                  ($condor_config_val = $1) =~ s/condor_submit/condor_config_val/;
              }
              if(/^condor_config\s*=\s*\"(.+)\"/) {
                  $condor_config = $1;
              }
          }

          if($condor_config_val and $condor_config) {
              $get_condor_config_cmd = "CONDOR_CONFIG=" . shellquote($condor_config, $condor_config_val, "PER_JOB_HISTORY_DIR");
          }
          else {
              setUnknown();
              &appendDetailsData (<<EOF);
Could not find condor_config_val command or CONDOR_CONFIG: could
not check for presence of PER_JOB_HISTORY_DIR attribute in condor
schedd configuration.
Please check manually.
EOF
              return 0;
              
          }
      }
  }

  my $cmd_output = `$get_condor_config_cmd 2>&1`;
  my $status = $?;
  if ($status == 0) {
    # Make sure we're not suffering from alias problems.
    chomp $cmd_output;
    $cmd_output = abs_path($cmd_output)
      if $cmd_output;
  }
  if ($status == 127 or $cmd_output =~ m&command not found&) {
    setUnknown();
    &appendDetailsData (<<EOF);
Unable to find condor_config_val command: could not check for presence
of PER_JOB_HISTORY_DIR attribute in condor schedd configuration.
$cmd_output
Please check manually.
EOF
    return 0;
  } elsif ($cmd_output =~ m&^Not defined:&i or $status != 0 or not $cmd_output) {
    setWarning();
    &appendDetailsData (<<EOF);
WARNING: PER_JOB_HISTORY_DIR apparently not set in condor_config or condor_config_val
not operating as expected. Please ensure it is set to "$datafolder" in
condor_config or the schedd machine's local configuration.
EOF
    return 0;
  } else {
    if ($status == 0 and $cmd_output ne $datafolder) {
      setWarning();
      &appendDetailsData (<<EOF);
WARNING: PER_JOB_HISTORY_DIR is apparently set to "$cmd_output"
in condor_config. It should be set to
"$datafolder" as specified in ProbeConfig.
Please rectify in condor_config or the schedd machine's local configuration.
EOF
      return 0;
    }
  }
  return 1;
}

sub probe_check_pbs_lsf {
  my $URCOLLECTOR_LOC = probeDirPath();
  push @INC, $URCOLLECTOR_LOC;
  eval "require urCollector::Configuration";
  if ($@) {                     # FAILED to load configuration module
    setCritical();
    &appendDetailsData (<<EOF);
ERROR: Could not load urCollector::Configuration: $@
Major probe installation problem.
EOF
    normal_exit();
  }
  eval
    urCollector::Configuration::parseConf("$URCOLLECTOR_LOC/urCollector.conf");
  if ($@) {                     # FAILED to parse configuration
    setCritical();
    &appendDetailsData (<<EOF);
ERROR: Could not read $URCOLLECTOR_LOC/urCollector.conf: $@
Please check urCollector.conf file for errors.
EOF
    normal_exit();
  }
  # Check LRMS type setting
  if ($urCollector::Configuration::configValues{lrmsType} !~ m&(?:pbs|lsf)&i) {
    setCritical();
    &appendDetailsData (<<EOF);
ERROR: Unrecognized lrmsType $urCollector::Configuration::configValues{lrmsType} in urCollector.conf
Please check urCollector.conf file for errors.
EOF
    normal_exit();
  }
  # Check LRMS type matches probe type
  if ($current_gratia_probe_type ne "pbs-lsf" and
      $urCollector::Configuration::configValues{lrmsType} ne $current_gratia_probe_type) {
    setCritical();
    &appendDetailsData (<<EOF);
ERROR: Mismatch between probeType $current_gratia_probe_type and configured lrmsType $urCollector::Configuration::configValues{lrmsType} in urCollector.conf.
Please check urCollector.conf file for errors.
EOF
    normal_exit();
  }
  unless ($is_root) {		# Can't do these unless we're root.
    $run_me_root = RMR_ALWAYS; # Print root message even if we're otherwise OK.
#    &appendDetailsData (<<EOF);
#Remaining checks on $urCollector::Configuration::configValues{lrmsType} require root privilege:
#  * Check existence of configured PBS or LSF log directory.
#  * Check for recent PBS batch log entries.
#  * Check pending XML files in $workingfolder/urCollector.
#EOF
    return;
  }
  # Check log directory
  if ($urCollector::Configuration::configValues{lrmsType} eq "pbs") {
    my $dir = $urCollector::Configuration::configValues{pbsAcctLogDir};
    my $date = UnixDate("today", "%Y%m%d");
    if (not -d $dir) {
      setCritical();
      &appendDetailsData (<<EOF);
ERROR: Configured PBS log directory $dir does not exist.
Check pbsAcctLogDir in urCollector.conf.
EOF
      normal_exit();
    }
    my $nfiles = globcount("\Q$dir\E/*");

    if (!$nfiles) {
      setCritical();
      &appendDetailsData (<<EOF);
ERROR: No files found in configured PBS log directory $dir.
Check pbsAcctLogDir in urCollector.conf.
EOF
      normal_exit();
    } elsif ((not -e "$dir/$date") and (not -e "$dir/$date.gz")) {
      setWarning();
      &appendDetailsData (<<EOF);
WARNING: Configured PBS log directory $dir does not contain entries for today.
Check pbsAcctLogDir in urCollector.conf and contents of $dir.
EOF
    }
  } elsif ($urCollector::Configuration::configValues{lrmsType} eq "lsf") {
    my $dir = $urCollector::Configuration::configValues{lsfAcctLogDir};
    if (not -d $dir) {
      setCritical();
      &appendDetailsData (<<EOF);
ERROR: Configured LSF log directory $dir does not exist.
Check lsfAcctLogDir in urCollector.conf.
EOF
      normal_exit();
    } elsif (not -e "$dir/lsb.events") {
      setWarning();
      &appendDetailsData (<<EOF);
WARNING: No log file $dir/lsb.events.
Check lsfAcctLogDir in urCollector.conf and contents of $dir.
EOF
    }
    # Check PBS files waiting to be converted to Gratia files
    my $npending = globcount("\Q$workingfolder\E/urCollector/*");
    &appendDetailsData ("$npending XML files waiting to be sent to $soaphost\n");
    if ($npending > 2000) {
      setWarning();
    }
  }
}

sub checkProbeDirs {
  my $probe_dir = probeDirPath();
  my $probe_config_dir = probeConfigPath();
  return (checkDirReadable($probe_dir, "$current_gratia_probe_type probe directory") and
          checkDirReadable($probe_config_dir, "$current_gratia_probe_type probe configuration directory") and
          checkFileReadable("$probe_config_dir/ProbeConfig", "$current_gratia_probe_type probe configuration"));
}

sub checkDirReadable {
  my ($dir, $description) = @_;
  if (-d $dir and -r $dir) {
    return 1;
  } else {
    &appendDetailsData (<<EOF);
DirectoryCheck: $dir ($description) does not exist or is not readable.
EOF
    return 0;
  }
}

sub checkFileReadable {
  my ($file, $description) = @_;
  if (-f $file and -r $file) {
    return 1;
  } else {
    &appendDetailsData (<<EOF);
FileCheck: $file ($description) does not exist or is not readable.
EOF
    return 0;
  }
}

sub getProbeConfigAttribute {
  my $probeConfigPath = probeConfigPath();
  my $quoted_probeConfigPath = shellquote($probeConfigPath);
  my $probeConfig_cmd = $ENV{OSG_LOCATION} ? 
      '( source "$OSG_LOCATION"/setup.sh;' . " cd $quoted_probeConfigPath; ../common/GetProbeConfigAttribute.py %s )" : 
      "( cd $quoted_probeConfigPath; /usr/share/gratia/common/GetProbeConfigAttribute %s )";

  my @results = ();

  foreach my $configAttribute (@_) {
    my $cmd = sprintf($probeConfig_cmd, $configAttribute);
    push @results, `$cmd 2>/dev/null`;
  }

  chomp @results;
  if (wantarray) {
    return @results;
  } else {
    return $results[0];
  }
}

sub gratiaProbeType {

  return $o{'gratiaProbeType'}; ## $o{'gratiaProbeTypes'}{$metric{'metricName'}} || "UNKNOWN";
}

sub probeDirPath {
    if($ENV{OSG_LOCATION}) {
        return ($real_gratia_probe_type) ? "$ENV{OSG_LOCATION}/gratia/probe/$real_gratia_probe_type" : "<unknown>";
    }
    return ($real_gratia_probe_type) ? "/usr/share/gratia/$real_gratia_probe_type" : "<unknown>";
}

sub probeConfigPath {
    if($ENV{OSG_LOCATION}) {
        return ($real_gratia_probe_type) ? "$ENV{OSG_LOCATION}/gratia/probe/$real_gratia_probe_type" : "<unknown>";
    }
    return ($real_gratia_probe_type) ? "/etc/gratia/$real_gratia_probe_type" : "<unknown>";
}

sub appendDetailsData {
    my $string = shift;
    $metric{'detailsData'} .= $string if ($string);
}

sub discoverProbesToTest {
  my $output = undef;

  my $probedir = (defined $ENV{OSG_LOCATION}) ? "$ENV{OSG_LOCATION}/gratia/" : "/usr/share/gratia";

  unless (-d $probedir) {
    workerScriptOutput(1,"", "ERROR: gratia-config-probe-helper: no directory $probedir!");
  }

  unless (opendir(DIR, $probedir)) {
    workerScriptOutput(1,"", "ERROR: gratia-config-probe-helper: unable to open directory for $probedir!");
  }

  # Execute probe once per probe directory.
  my @dirs = ();
  while (my $dir = readdir DIR) {
    next unless -d "$probedir/$dir";
    next if $dir =~ m&^\.+$&;
    next if $dir =~ m&^(?:test|common)$&;
    push @dirs, $dir;
  }
  closedir(DIR);
  if ($options{'human-readable'}) {
    $output = "Found probes: \n";
    foreach my $dir (@dirs) {
      $output = "${output}$dir\n";
    }
  } else {
    $output = "<probes>\n";
    foreach my $dir (@dirs) {
      $output = "${output}  <probe>$dir</probe>\n";
      $o{'gratiaProbeTypes'}{"org.osg.gratia.$dir"} = $dir;
    }
    $output .= "</probes>";
  }
  workerScriptOutput(0,$output,"");
}


## Print exit code, stdout, stderr in format expected by rsv-control
sub workerScriptOutput {
    if ($options{'human-readable'}) {
        my $probe_dir = probeDirPath();
        print <<EOF;
Exit code: $_[0]
EOF
        if ($current_gratia_probe_type) {
            print <<EOF;
Probe type: $current_gratia_probe_type
Probe dir: $probe_dir
Diagnostic output:
EOF
        }
        print "$_[1]" if $_[1];
        print "$_[2]" if $_[2];
    }
    else {
        print metricStatusWord() . "\n";
        print $_[1];
        print STDERR $_[2];
    }

    exit 0;
}

use File::Find;
sub init_find_counts {
  $record_count = $file_count = $archive_count = $old_file_count = 0;
}

sub process_find_leaf {
  -f or return;
  my $uncompress_arg;
  if (m&\.tar(?:\.(bz2|gz))?$&) {
    ++$archive_count;
    $uncompress_arg = ($1?(($1 eq "bz2")?"j":"z"):"");
    my $quoted_filename = shellquote($File::Find::name);
    my $tar_count = `(tar ${uncompress_arg}tvf $quoted_filename | grep -ve '^d' | wc -l) 2>/dev/null`;
    chomp $tar_count;
    if ($tar_count and $tar_count > 0) {
      $record_count += $tar_count;
    }
  } else {
    ++$record_count;
  }
  ++$file_count;
  ++$old_file_count if int(-M _) > 7;
}

# based on ProbeConfiguration.getFilenameFragment() from gratia/common/probe_config.py
# Gratia version 1.13.9, 2013-03-29
sub ProbeConfiguration_getFilenameFragment {
    my ($probename, $soaphost) = @_;
    my $fragment = "${probename}_$soaphost";
    $fragment =~ s{[:/]}{_}g;
    return $fragment;
}

# vim: ft=perl:sw=2:sts=2
### Local Variables:
### mode: cperl
### End:
